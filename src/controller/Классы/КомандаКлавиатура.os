#Использовать oint

&Деталька("TELEGRAMM.TOKEN")
Перем Токен;

&Пластилин
Перем МенеджерПубликаций;

&ЛогПубликация
Перем Лог;

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Функция ПодходитПоУсловию(Сообщение) Экспорт

	Возврат Сообщение.Получить("callback_query") <> Неопределено;

КонецФункции


Процедура Обработать(Сообщение) Экспорт
	
	Лог.Отладка(OPI_Инструменты.JSONСтрокой(Сообщение, , Ложь, Ложь));	
	НомерСообщения = Сообщение["update_id"];	

	СообщениеНажатиеКнопки = Сообщение["callback_query"]["message"];
	ЧатИД = СообщениеНажатиеКнопки["chat"]["id"];
	ИДПользователя = СообщениеНажатиеКнопки["chat"]["id"]; //Надо разобраться почему тут подставляется не ид пользователя ["message"]["id"]

	Клавиши = СообщениеНажатиеКнопки["reply_markup"]["inline_keyboard"];

	КнопкаНовая = Клавиши[Клавиши.ВГраница()][0]["callback_data"];
	
	ИнформацияОФайле = МенеджерПубликаций.ИнформацияОФайлеПоИдентификатору(Число(СтрЗаменить(КнопкаНовая, "NEW_", "")));
	НажатаяКнопка = Сообщение["callback_query"]["data"];
	
	ДвоичныеДанные = OPI_Telegram.СкачатьФайл(Токен, ИнформацияОФайле.ИДФайла);	
		
	ДанныеДляРазбораФайла = МенеджерПубликаций.НовыйДанныеДляРазбораФайла();	
	Если КнопкаНовая <> НажатаяКнопка Тогда
		ИнформацияОПубликации = МенеджерПубликаций.ИнформацияОПубликацииПоИдентификатору(Число(НажатаяКнопка));
		ДанныеДляРазбораФайла.Обновление = Истина;
		ДанныеДляРазбораФайла.ИДПубликации = ИнформацияОПубликации.ИДПубликации;
	Иначе 
		ДанныеДляРазбораФайла.Обновление = Ложь;	
	КонецЕсли;	
	
	ДанныеДляРазбораФайла.ИДПользователя = ИДПользователя;
	ДанныеДляРазбораФайла.НомерСеанса = НомерСообщения;
	ДанныеДляРазбораФайла.ДвоичныеДанные = ДвоичныеДанные;	
	ДанныеДляРазбораФайла.ИДФайла = ИнформацияОФайле.ИДФайла;
	ДанныеДляРазбораФайла.ИмяФайла = ИнформацияОФайле.ИмяФайла;

	ТекстОтвета = МенеджерПубликаций.ОбработатьФайл(ДанныеДляРазбораФайла);
	OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтвета);
	

КонецПроцедуры
