#Использовать oint
&Деталька("TELEGRAMM.TOKEN")
Перем Токен;

&Деталька("FILE_MAX_SIZE")
Перем МаксимальныйРазмерФайла;

&Пластилин
Перем МенеджерПубликаций;


&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция ПодходитПоУсловию(Сообщение) Экспорт
	
	Подходит = Сообщение.Получить("message") <> Неопределено И Сообщение["message"].Получить("document") <> Неопределено;
	Возврат Подходит;
	
КонецФункции

Процедура Обработать(Сообщение) Экспорт
	
	ЧатИД = Сообщение["message"]["chat"]["id"];
	ПользовательИД = Сообщение["message"]["from"]["id"];
	ИмяФайла = Сообщение["message"]["document"]["file_name"];
	ИДФайла = Сообщение["message"]["document"]["file_id"];

	Попытка
		ФайлВалидный(Сообщение);
	Исключение
		Информация = ИнформацияОбОшибке();
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, Информация.Описание);
	КонецПопытки;

	ОтправляемыйФайл = МенеджерПубликаций.НовыйОтправляемыйФайл();
	ОтправляемыйФайл.ПользовательИД = ПользовательИД;
	ОтправляемыйФайл.ИмяФайла = ИмяФайла;	
	ОтправляемыйФайл.ИДФайла = ИДФайла;
	
	ПрошлыеПубликации = МенеджерПубликаций.ПолучитьСписокПубликаций(ОтправляемыйФайл);
	Если ПрошлыеПубликации.Количество() Тогда
		
		ТекстОтвета = "Ранее вы уже публиковали файл с таким именем, заменить или выложить еще раз?";
		Клавиатура = СформироватьКлавиатуруПоМассивуКнопок(ПрошлыеПубликации);
		МенеджерПубликаций.СохранитьИнформациюОФайле(ОтправляемыйФайл);
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтвета, Клавиатура);
		Возврат;
		
	КонецЕсли;

	ДвоичныеДанные = OPI_Telegram.СкачатьФайл(Токен, ИДФайла);	
	НомерСообщения = Сообщение["update_id"];

	ДанныеДляРазбораФайла = МенеджерПубликаций.НовыйДанныеДляРазбораФайла();	
	ДанныеДляРазбораФайла.ПользовательИД = ПользовательИД;
	ДанныеДляРазбораФайла.ИмяФайла = ИмяФайла;
	ДанныеДляРазбораФайла.ИДПубликации = НомерСообщения;
	ДанныеДляРазбораФайла.ДвоичныеДанные = ДвоичныеДанные;	
	ДанныеДляРазбораФайла.ИДФайла = ИДФайла;
	ДанныеДляРазбораФайла.Обновление = Ложь;

	ТекстОтвета = МенеджерПубликаций.ОбработатьФайл(ДанныеДляРазбораФайла);
	OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтвета);
	
	Сообщить(ТекстОтвета);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьКлавиатуруПоМассивуКнопок(Кнопки)
	
	Строки = СоздатьВысокуюКлавиатуру(Кнопки);
	СтруктураПараметра = Новый Структура("inline_keyboard, rows", Строки, 1);
	СтруктураПараметра = OPI_Инструменты.JSONСтрокой(СтруктураПараметра, , Ложь, Ложь);
	Возврат СтруктураПараметра;
	
КонецФункции

Функция СоздатьВысокуюКлавиатуру(Кнопки)
	
	Строки = Новый Массив;
	
	Для Каждого Кнопка Из Кнопки Цикл
		Кнопки = Новый Массив;
		Кнопки.Добавить(Новый Структура("text, callback_data", Кнопка.Ключ, Кнопка.Значение));
		Строки.Добавить(Кнопки);
	КонецЦикла;
	
	Возврат Строки;
	
КонецФункции

Процедура ФайлВалидный(Сообщение)
	
	РазмерФайла = Сообщение["message"]["document"]["file_size"];
	ИмяФайла = Сообщение["message"]["document"]["file_name"];
	Если РазмерФайла > МаксимальныйРазмерФайла Тогда
		ВызватьИсключение "Мы пока не принимаем больше файлы";
	КонецЕсли;
		               
	Расширение = СтрРазделить(ИмяФайла, ".");
	Если НЕ РаботаСМодулями.ДопустимоеРасширение(Расширение[Расширение.ВГраница()]) Тогда
		ВызватьИсключение "Текущее расширение файла не поддерживатеся";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти