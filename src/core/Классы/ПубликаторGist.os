#Использовать 1connector

#Область ОбъявлениеПеременных

&Деталька("GIST.TOKEN")
Перем Токен;

&Деталька("GIST.NAME")
Перем Имя;

&ЛогПубликация
Перем Лог;

#КонецОбласти

&Желудь
&Прозвище("Публикатор")
&Реализует("ИнтерфейсПубликатор")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Функция Опубликовать(ТекстыМодулей, ДанныеДляРазбораФайла) Экспорт
	
	Ссылки = Новый Соответствие();
	АдресРесурса = АдресРесурса();
	
	Заголовки = Новый Структура();
	Заголовки.Вставить("Authorization", "token " + Токен);
	
	ФайлыДляAPI = Новый Соответствие();
	Для Каждого Файл Из ТекстыМодулей Цикл
		Если НЕ ЗначениеЗаполнено(Файл.Значение) Тогда
			Лог.Отладка("Файл не содержит данных для публикации: " + Файл.Ключ);
			Продолжить;
		КонецЕсли;
		ФайлДанные = Новый Соответствие();
		ФайлДанные.Вставить("content", Файл.Значение);
		ФайлыДляAPI.Вставить(Файл.Ключ, ФайлДанные);
		
	КонецЦикла;
	
	Тело = Новый Соответствие();
	Тело.Вставить("files", ФайлыДляAPI);
	Тело.Вставить("public", Ложь);
	Тело.Вставить("description", "Gist создан из 1С " + ТекущаяДата());
	
	Если ДанныеДляРазбораФайла.Обновление Тогда
		Ответ = КоннекторHTTP.Patch(СтрШаблон("%1/%2", АдресРесурса, ДанныеДляРазбораФайла.ИДПубликации),
				ОбъектВJson(Тело), Новый Структура("Заголовки", Заголовки)
			);
	Иначе
		Ответ = КоннекторHTTP.Post(АдресРесурса, , Тело, Новый Структура("Заголовки", Заголовки));
	КонецЕсли;
	
	Если КодСостоянияУспешный(Ответ.КодСостояния) Тогда
		ОтветJson = Ответ.Json();
	Иначе
		Лог.Ошибка("Не удалось опубликовать файл в Gist: Код состояния" + Ответ.КодСостояния);
		ВызватьИсключение "Не удалось опубликовать файл в Gist: Код состояния" + Ответ.КодСостояния;
	КонецЕсли;
	
	Оглавление = ОглавлениеПоДаннымGist(ОтветJson);
	АдресРесурса = СтрШаблон("%1/%2", АдресРесурса, ОтветJson["id"]);
	
	ФайлДанные = Новый Соответствие();
	ФайлДанные.Вставить("content", Оглавление);
	ФайлыДляAPI.Вставить("Contents.md", ФайлДанные);
	
	Тело = Новый Соответствие();
	Тело.Вставить("files", ФайлыДляAPI);
	Тело.Вставить("public", Ложь);
	Тело.Вставить("description", "Gist создан из 1С " + ТекущаяДата());
	
	Ответ = КоннекторHTTP.Patch(АдресРесурса, ОбъектВJson(Тело), Новый Структура("Заголовки", Заголовки));
	Если КодСостоянияУспешный(Ответ.КодСостояния) Тогда
		ОтветJson = Ответ.Json();
		Ссылки.Вставить(ОтветJson["id"], ОтветJson["html_url"] + "#file-contents-md");
	КонецЕсли;
	
	Возврат Ссылки;
	
КонецФункции

Функция АдресРесурса()
	
	Возврат "https://api.github.com/gists";
	
КонецФункции

Функция ОбъектВJson(Объект)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Объект);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

//Что бы все нахрен грохнуть после отладки и эксперементов
Процедура УдалитьВсеПубликации() Экспорт
	
	Заголовки = Новый Структура();
	Заголовки.Вставить("Authorization", "token " + Токен);
	Адрес = СтрШаблон("https://api.github.com/users/%1/gists", Имя);
	Гисты = КоннекторHTTP.Get(Адрес, , Новый Структура("Заголовки", Заголовки)).Json();
	
	Для Каждого Гист Из Гисты Цикл
		Ответ = КоннекторHTTP.delete("https://api.github.com/gists/" + Гист["id"], , ,
				Новый Структура("Заголовки", Заголовки)
			);
	КонецЦикла;
	
КонецПроцедуры

Функция КодСостоянияУспешный(КодСостояния)
	
	КодыОтвета = Новый Массив;
	КодыОтвета.Добавить(200);
	КодыОтвета.Добавить(201);
	
	Возврат КодыОтвета.Найти(КодСостояния) <> Неопределено;
	
КонецФункции

Функция ОглавлениеПоДаннымGist(Данные)
	
	Дерево = РаботаСМодулями.ДеревоОбъектов(Данные);
	
	Результат = "# Дерево файлов
		|
		|";
	
	Для Каждого КлючИЗначение Из Дерево Цикл
		ТипОбъекта = КлючИЗначение.Ключ;
		Объекты = КлючИЗначение.Значение;
		
		ТекстовоеОписаниеОбъектов = Новый Массив;
		Для Каждого КлючИЗначение2 Из Объекты Цикл
			ИмяОбъекта = КлючИЗначение2.Ключ;
			Модули = КлючИЗначение2.Значение;
			СсылкиНаМодули = Новый Массив();
			Для Каждого Модуль Из Модули Цикл
				СсылкиНаМодули.Добавить(СтрШаблон("- [%1](%2)", Модуль.Описание, Модуль.Ссылка));
			КонецЦикла;
			
			ТекстовоеОписаниеОбъектов.Добавить(СтрШаблон("
					| %1
					|%2
					|", ИмяОбъекта, СтрСоединить(СсылкиНаМодули, Символы.ПС)));
			
		КонецЦикла;
		
		Результат = Результат + СтрШаблон("
				|<details>
				| <summary><strong> %1 </strong></summary>
				|%2
				|</details>", ТипОбъекта, СтрСоединить(ТекстовоеОписаниеОбъектов, Символы.ПС));
		
	КонецЦикла;
	Возврат Результат;
	
КонецФункции