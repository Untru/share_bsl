#Использовать 1connector

&Деталька("GIST.TOKEN")
Перем Токен;

&Желудь
&Прозвище("Публикатор")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Функция Опубликовать(ТекстыМодулей) Экспорт
	
	Ссылки = Новый Массив;
	АдресРесурса = АдресРесурса();

  	Заголовки = Новый Структура();
    Заголовки.Вставить("Authorization", "token " + Токен);


	ФайлыДляAPI = Новый Соответствие();
	Для Каждого Файл Из ТекстыМодулей Цикл
	      
		ФайлДанные = Новый Соответствие();
        ФайлДанные.Вставить("content", Файл.Значение);
        ФайлыДляAPI.Вставить(Файл.Ключ, ФайлДанные);

	КонецЦикла;
    
	Тело = Новый Соответствие();
    Тело.Вставить("files", ФайлыДляAPI);
    Тело.Вставить("public", Ложь);
	Тело.Вставить("description", "Gist создан из 1С " + ТекущаяДата());

	Ответ = КоннекторHTTP.Post(АдресРесурса,, Тело, Новый Структура("Заголовки", Заголовки));
	Если Ответ.КодСостояния = 201 Тогда
		ОтветJson = Ответ.Json();
		Ссылки.Добавить(ОтветJson["html_url"]);
	КонецЕсли;
	
	Возврат Ссылки;

КонецФункции

Функция АдресРесурса()

	Возврат "https://api.github.com/gists";

КонецФункции
