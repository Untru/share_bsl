#Использовать oint

&Деталька("TELEGRAMM.TOKEN")
Перем Токен;

&Пластилин
Перем Поделка;

&Пластилин
Перем ОпубликованныеGist;

&Пластилин
Перем Декомпилятор;

Перем ПользовательИД;

&Пластилин
Перем МенеджерСущностей;

&Пластилин
Перем ХранилищеСущностейИсторияПубликаций;

&Лог(Значение = "share_bsl.logger.DEBUG", УчитыватьИмяКласса = Ложь)
Перем Лог;

&Пластилин
Перем СервисыПубликации;

&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&Асинх
Процедура ОбработатьСообщение(ПолноеСообщение) Экспорт

	Если ПолноеСообщение.Получить("message") <> Неопределено Тогда
		ПользовательИД = ПолноеСообщение["message"]["from"]["id"];
		ЧатИД = ПолноеСообщение["message"]["chat"]["id"];
	Иначе
		Возврат;
	КонецЕсли;

	НомерСообщения = ПолноеСообщение["update_id"];
	Сообщение = ПолноеСообщение["message"];
	//TODO Сделать "caption": "@sharebsl_bot"
	
	Если Сообщение.Получить("document") = Неопределено Тогда
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Вы не отправили файл");
		Возврат;
	КонецЕсли;
	ИмяФайла = Сообщение["document"]["file_name"];
	IDФайла = Сообщение["document"]["file_id"];
	РазмерФайла = Сообщение["document"]["file_size"];
	Если РазмерФайла > 30000000 Тогда //Ограничение файла до 30 мб
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Мы пока не принимаем файлы больше 30 мб");
		Возврат;
	КонецЕсли;
		               
	Расширение = СтрРазделить(ИмяФайла, ".");
	Если НЕ РаботаСМодулями.ДопустимоеРасширение(Расширение[Расширение.ВГраница()]) Тогда
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Такой файл мы не можем разобрать");
		Возврат;
	КонецЕсли;
	
	Сущности = ОпубликованныеGist.НайтиЗаписьОФайле(ПользовательИД, ИмяФайла);
	Если Сущности.Количество() Тогда
		МассивКлавиатуры = Новый Массив;
		Для каждого Сущность Из Сущности Цикл
			МассивКлавиатуры.Добавить(Строка(Сущность.ДатаОтправки));	
		КонецЦикла;
		МассивКлавиатуры.Добавить("Сделать новую публикацию");	
 		ТекстОтвета = "Ранее вы уже публиковали файл с таким именем, заменить или выложить еще раз?";
        Клавиатура = OPI_Telegram.СформироватьКлавиатуруПоМассивуКнопок(МассивКлавиатуры, Истина);
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтвета, Клавиатура);
		Возврат;
	КонецЕсли;

	// ОпубликованныеGist.СохранитьЗаписьОФайле(ПользовательИД, ИмяФайла);	

	КаталогBin = "bin/" + НомерСообщения;
	СоздатьКаталог(КаталогBin);
	КаталогResources = "Resources/" + НомерСообщения;
	СоздатьКаталог(КаталогResources);
	ПутьКФайлу = СтрШаблон("%1/%2", КаталогBin, ИмяФайла);
	
	Двоичные = OPI_Telegram.СкачатьФайл(Токен, IDФайла);
	Двоичные.Записать(ПутьКФайлу);
	
	Декомпилятор.РазобратьФайл(ПутьКФайлу, КаталогResources);
	Попытка
		ТекстыМодулей = РаботаСМодулями.ТекстыМодулей(КаталогResources);
		
		Публикаторы = Поделка.НайтиЖелуди("Публикатор", , "Соответствие");
		Для Каждого Публикатор Из Публикаторы Цикл
			
			Если СервисыПубликации[Публикатор.Ключ] = Неопределено Тогда
				Возврат;
			КонецЕсли;
			Ссылки = Публикатор.Значение.Опубликовать(ТекстыМодулей);
			ТекстыОтправки = Новый Массив;
			Если Ссылки.Количество() Тогда
				Для Каждого Ссылка Из Ссылки Цикл
					ТекстыОтправки.Добавить(СтрШаблон("Ссылка на код: %1", Ссылка.Значение));
				КонецЦикла;
				СохранитьЗаписьОФайле(ПользовательИД, ИмяФайла, Ссылка.Ключ); 	
				ТекстОтправки = СтрСоединить(ТекстыОтправки, Символы.ПС);
			Иначе
				ТекстОтправки = "Не найдено bsl файлов";
			КонецЕсли;
			OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтправки);
		КонецЦикла;	
		
	Исключение
		ОшибкаВыполненияЗапроса = ИнформацияОбОшибке();
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ОшибкаВыполненияЗапроса);
		Сообщить(ОписаниеОшибки);
		Лог.Ошибка(ОписаниеОшибки);
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ОписаниеОшибки);
	КонецПопытки;
	
	УдалитьФайлы(КаталогResources);
	УдалитьФайлы(КаталогBin);
	
КонецПроцедуры

Процедура СохранитьЗаписьОФайле(ИДПользователя, ИмяФайла, ИДПубликации) Экспорт
	
	ИсторияПубликаций = ХранилищеСущностейИсторияПубликаций.СоздатьЭлемент();
	ИсторияПубликаций.ИДПользователя = ИДПользователя;
	ИсторияПубликаций.ИмяФайла = ИмяФайла;	
	ИсторияПубликаций.ДатаОтправки = ТекущаяДата();	
	ИсторияПубликаций.ИДПубликации = ИДПубликации;
	ИсторияПубликаций.Сохранить();

КонецПроцедуры