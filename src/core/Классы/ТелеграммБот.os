#Использовать oint

&Деталька("TELEGRAMM.TOKEN")
Перем Токен;

&Пластилин
Перем Поделка;

&Пластилин
Перем Декомпилятор;

&Пластилин
Перем СервисыПубликации;

&Число
Перем Смещение;

&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Запустить() Экспорт
	Сообщить("Запущен");
	
	Пока Истина Цикл
		
		Ответ = OPI_Telegram.ПолучитьОбновления(Токен, 30, Смещение);
		Результат = Ответ["result"];
		
		Если Результат = Неопределено ИЛИ Результат.ВГраница() = -1 Тогда
			Продолжить;
		Иначе
			Сообщение = Результат[Результат.ВГраница()];
			Смещение = Сообщение["update_id"] + 1;
		КонецЕсли;
		
		ОбработатьРезультат(Сообщение);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьРезультат(ПолноеСообщение)
	
	Если ПолноеСообщение.Получить("message") <> Неопределено Тогда
		ПользовательИД = ПолноеСообщение["message"]["from"]["id"];
		ЧатИД = ПолноеСообщение["message"]["chat"]["id"];
	Иначе
		Возврат;
	КонецЕсли;
	
	НомерСообщения = ПолноеСообщение["update_id"];
	Сообщение = ПолноеСообщение["message"];
	//TODO Сделать "caption": "@sharebsl_bot"
	
	Если Сообщение.Получить("document") = Неопределено Тогда
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Вы не отправили файл");
		Возврат;
	КонецЕсли;
	ИмяФайла = Сообщение["document"]["file_name"];
	IDФайла = Сообщение["document"]["file_id"];
	
	Расширение = СтрРазделить(ИмяФайла, ".");
	Если НЕ ОбщегоНазначения.ДопустимоеРасширение(Расширение[Расширение.ВГраница()]) Тогда
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, "Такой файл мы не можем разобрать");
		Возврат;
	КонецЕсли;
	
	КаталогBin = "bin/" + НомерСообщения;
	СоздатьКаталог(КаталогBin);
	КаталогResources = "Resources/" + НомерСообщения;
	СоздатьКаталог(КаталогResources);
	ПутьКФайлу = СтрШаблон("%1/%2", КаталогBin, ИмяФайла);
	
	Двоичные = OPI_Telegram.СкачатьФайл(Токен, IDФайла);
	Двоичные.Записать(ПутьКФайлу);
	
	Декомпилятор.РазобратьФайл(ПутьКФайлу, КаталогResources);
	Попытка
		ТекстыМодулей = ОбщегоНазначения.ТекстыМодулей(КаталогResources);
		
		Публикаторы = Поделка.НайтиЖелуди("Публикатор", , "Соответствие");
		Для Каждого Публикатор Из Публикаторы Цикл
			
			Если СервисыПубликации[Публикатор.Ключ] = Неопределено Тогда
				Возврат;
			КонецЕсли;
			Ссылки = Публикатор.Опубликовать(ТекстыМодулей);
			Если Ссылки.Количество() Тогда
				ТекстОтправки = "Ссылки на код: " + СтрСоединить(Ссылки, Символы.ПС);
			Иначе
				ТекстОтправки = "Не найдено bsl файлов";
			КонецЕсли;
			OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ТекстОтправки);
		КонецЦикла;	
		
	Исключение
		ОшибкаВыполненияЗапроса = ИнформацияОбОшибке();
		OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, ЧатИД, ПодробноеПредставлениеОшибки(ОшибкаВыполненияЗапроса));
	КонецПопытки;
	
	УдалитьФайлы(КаталогResources);
	УдалитьФайлы(КаталогBin);
	
КонецПроцедуры